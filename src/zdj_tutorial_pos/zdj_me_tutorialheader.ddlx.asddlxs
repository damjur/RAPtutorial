@Metadata.layer: #CORE
@Search.searchable: true
@UI: {
  headerInfo: {
        typeName: 'Receipt',
        typeNamePlural: 'Receipts',
        title:{
            value: 'Description',
            type: #STANDARD
        },
        description:{
            value: 'Id',
            type: #STANDARD
        },
        imageUrl: 'PictureURL'
  },
  presentationVariant: [{
    requestAtLeast: ['ModifiedAt'],
    sortOrder: [{
        by: 'ModifiedAt',
        direction:  #DESC
    }],
    visualizations: [{type: #AS_LINEITEM}]
  }]

}
annotate entity ZDJ_C_TutorialHeader with
{
  @UI.facet: [
    { purpose: #STANDARD, label: 'Details', type : #FIELDGROUP_REFERENCE, targetQualifier: 'details'},
    { purpose: #STANDARD, label:'Items', type : #LINEITEM_REFERENCE, targetElement: '_Items' },
    
    { purpose: #HEADER, type : #FIELDGROUP_REFERENCE,targetQualifier: 'control', position: 1},
    { targetQualifier: 'status', type: #DATAPOINT_REFERENCE, purpose: #HEADER, position: 2 },
    { targetQualifier: 'price', type: #DATAPOINT_REFERENCE, purpose: #HEADER, position: 3 }
  ]
  @UI.identification: [{ type: #FOR_ACTION, dataAction: 'changeDescription', label: 'Change Description' },{ type: #FOR_ACTION, dataAction: 'cancel', label: 'Cancel' }]
  @UI.lineItem: [{ position: 10, criticality: 'Criticality' }]
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.5
  Id;
  @UI.lineItem: [{ position: 20 }]
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.7
  Description;
  @UI.fieldGroup: [{ qualifier: 'details' , position: 10 }]
  @EndUserText.label: 'Currency'
  @UI.selectionField: [{position: 20}]
  @Consumption.valueHelpDefinition: [{ entity:{ name: 'I_CurrencyStdVH', element: 'Currency' } }]
  Currency;
  @UI.lineItem: [{ position: 30 }]
  @EndUserText.label: 'Customer'
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.7
  Customer;
  @UI.fieldGroup: [{ qualifier: 'details' , position: 30 }]
  @UI.selectionField: [{position: 30}]
  @Consumption.valueHelpDefinition: [{ entity:{ name: 'ZDJ_I_TutposStatusVH', element: 'Code' } }]
  Status;
  @UI.fieldGroup: [{ qualifier: 'control' , position: 10 }]
  @EndUserText.label: 'Created By'
  Creator;
  @UI.fieldGroup: [{ qualifier: 'control' , position: 20 }]
  @EndUserText.label: 'Created At'
  CreatedAt;
  @UI.fieldGroup: [{ qualifier: 'control' , position: 30 }]
  @EndUserText.label: 'Modified By'
  Modificator;
  @UI.fieldGroup: [{ qualifier: 'control' , position: 40 }]
  @UI.lineItem: [{ position: 99 }]
  @EndUserText.label: 'Modified At'
  @UI.selectionField: [{position: 10}]
  ModifiedAt;
  @UI.dataPoint:{
    title: 'Overall Price',
    qualifier: 'price'
  }
  @EndUserText.label: 'Overall Price'
  OverallPrice;
  @UI.lineItem: [{ position: 40 }]
  @EndUserText.label: 'Status Description'
  @UI.dataPoint:{
    title: 'Status',
    qualifier: 'status',
    criticality: 'Criticality'
  }
  StatusText;
  @UI.hidden: true
  Criticality;
  @UI.lineItem: [{ importance: #HIGH }]
  PictureURL;
}