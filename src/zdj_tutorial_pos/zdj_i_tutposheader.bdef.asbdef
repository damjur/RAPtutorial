managed;
strict;
with draft;

define behavior for ZDJ_I_TutposHeader alias TutposHeader
implementation in class zbp_dj_i_tutposheader unique
persistent table zdj_tut_pos_head
lock master total etag ModifiedAt
authorization master ( global, instance )
draft table zdj_tutpos_dhead
etag master ModifiedAt
{
  create;
  update;
  delete;

  draft action Activate;
  draft action Discard;
  draft action Edit;
  draft action Resume;

  draft determine action Prepare
  {
    validation validateItemsExist;
    validation validateCurrency;
    validation TutposItem~validateUnit;
  }

  field ( numbering : managed, readonly ) Id;
  field ( readonly ) Creator, CreatedAt, Modificator,
    ModifiedAt, Status, PictureURL, OverallPrice;
  field ( mandatory ) Currency;
  field ( readonly : update ) Description;

  action ( features : instance ) cancel result [1]  $self  ;
  action ( features : instance ) changeDescription parameter ZDJ_I_TutposInput result [1] $self;

  determination controlFieldsOnCreate on save { create; }
  determination controlFieldsOnUpdate on save { create; update; }
  determination recalculateOveravllPrice on modify { field Currency; }

  validation validateItemsExist on save { create; delete; update; }
  validation validateCurrency on save { field Currency; create; update; }

  association _Items { create; with draft; }

  mapping for zdj_tut_pos_head control zdj_tutpos_chead corresponding
  {
    CreatedAt = created_at;
    ModifiedAt = modified_at;
  }
}

define behavior for ZDJ_I_TutposItem alias TutposItem
implementation in class zbp_dj_i_tutpositem unique
persistent table zdj_tut_pos_item
lock dependent by _Header
authorization dependent by _Header
etag dependent by _Header
draft table zdj_tutpos_ditem
{
  update;
  delete;

  field ( numbering : managed, readonly ) Item;
  field ( readonly ) Id, Price;
  field ( mandatory ) Quantity, Unit, UnitPrice;

  association _Header { with draft; }

  internal action recalcPriceInt;

  validation validateUnit on save { field Unit; create; update; }

  determination onCreate on modify { create; }
  determination recalcPrice on modify { field UnitPrice, Unit, Quantity; }

  mapping for zdj_tut_pos_item control zdj_tutpos_citem corresponding
  {
    UnitPrice = unit_price;
  }
}